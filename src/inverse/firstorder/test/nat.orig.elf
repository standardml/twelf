
nat : type.

z : nat.
s : nat -> nat.

plus : nat -> nat -> nat -> type.
plus/z : plus z X X.
plus/s : plus X Y Z -> plus (s X) Y (s Z).

pluszr : {N} plus N z N -> type.
%mode pluszr +N -D.

- : pluszr z plus/z.
- : pluszr (s X) (plus/s D)
     <- pluszr X D.

%worlds () (pluszr _ _).
%total X (pluszr X _).

plussr : plus X Y Z -> plus X (s Y) (s Z) -> type.
%mode plussr +D -E.

- : plussr plus/z plus/z.
- : plussr (plus/s (D : plus X Y Z)) (plus/s E)
     <- plussr D E.

%worlds () (plussr _ _).
%total D (plussr D _).

plusC : plus X Y Z -> plus Y X Z -> type.
%mode plusC +D -E.

- : plusC plus/z D
     <- pluszr _ D.
- : plusC (plus/s (D : plus X Y Z)) E
     <- plusC D D'
     <- plussr D' E.

%worlds () (plusC _ _).
%total X (plusC X _).

eq : nat -> nat -> type.
eq/refl : eq X X.

plusA : plus X Y XY -> plus XY Z XYZ -> plus Y Z YZ -> plus X YZ XYZ -> type.
%mode plusA +D1 +D2 -D3 -D4.

- : plusA plus/z D D plus/z. 
- : plusA (plus/s D1) (plus/s D2) E1 (plus/s E2)
     <- plusA D1 D2 E1 E2.

% - : plusA plus/z D D plus/z. 
% - : plusA (plus/s (D1 : plus X Y XY) : plus (s X) Y (s XY))
%           (plus/s (D2 : plus XY Z XYZ) : plus (s XY) Z (s XYZ))
%           E1
%           (plus/s E2 : plus (s X) YZ (s XYZ))
%      <- plusA D1 D2 (E1 : plus Y Z YZ) (E2 : plus X YZ XYZ). 

%worlds () (plusA _ _ _ _).
%total X (plusA X _ _ _).


%.

Cl: (x + y) + z = x + (y + z)
Pf: 0 + y = y + z = 0 + (y + z)
  : 

H : (x + y) + z = x + (y + z)
TS: (s x + y) + z = s x + (y + z)

= s(x + y) + z
              

s x + (y + z) = s (x + (y + z))
              = s ((x + y) + z)
              = s (x + y) + z
              = (s x + y) + z 

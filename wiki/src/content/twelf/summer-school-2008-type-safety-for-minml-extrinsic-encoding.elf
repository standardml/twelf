%%! title: "Summer school 2008:Type safety for MinML (extrinsic encoding)"

%{! 

\{\{Summer school 2008
  |prev=Type safety for MinML (intrinsic encoding)
  |prevname=Type safety for MinML (intrinsic encoding)
  |next=Exercises 3
  |nextname=Exercises 3
\}\}

Type safety for MinML: call-by-value, with recursive functions, in extrinsic form

## Syntax

Types:

!}%

tp : type.				%name tp T.
nat : tp.
arr : tp -> tp -> tp.

%{! 

Raw expressions, which admit ill-typed terms

!}%

exp : type.				%name exp E.
z : exp.
s : exp -> exp.
ifz : exp -> exp -> (exp -> exp) -> exp.
fun : tp -> tp -> (exp -> exp -> exp) -> exp.
app : exp -> exp -> exp.

%{! 

## Static semantics

A judgement picking out the well-typed terms:

!}%

of : exp -> tp -> type.			%name of Dof.
%mode of +E -T.

of/z
   : of z nat.
of/s
   : of (s E) nat <- of E nat.
of/ifz
   : of (ifz E E1 ([x] E2 x)) T
      <- of E nat
      <- of E1 T
      <- ({x:exp} of x nat -> of (E2 x) T).
of/fun
   : of (fun T1 T2 ([f] [x] E f x)) (arr T1 T2)
      <- ({f:exp} of f (arr T1 T2) -> {x:exp} of x T1 -> of (E f x) T2).
of/app
   : of (app E1 E2) T
      <- of E1 (arr T2 T)
      <- of E2 T2.

%{! 

## Dynamic semantics

!}%

value : exp -> type.			%name value Dval.
%mode value +E.

value/z : value z.
value/s : value (s E) <- value E.
value/fun : value (fun _ _ _).

step : exp -> exp -> type.		%name step Dstep.
%mode step +E1 -E2.

step/s
   : step (s E) (s E') <- step E E'.
step/ifz/arg
   : step (ifz E E1 ([x] E2 x)) (ifz E' E1 ([x] E2 x))
      <- step E E'.
step/ifz/z
   : step (ifz z E1 ([x] E2 x)) E1.
step/ifz/s
   : step (ifz (s E) E1 ([x] E2 x)) (E2 E)
      <- value E.
step/app/fun
   : step (app E1 E2) (app E1' E2)
      <- step E1 E1'.
step/app/arg
   : step (app E1 E2) (app E1 E2')
      <- value E1 <- step E2 E2'.
step/app/beta-v
   : step
      (app (fun T1 T2 ([f] [x] E f x)) E2)
      (E (fun T1 T2 ([f] [x] E f x)) E2)
      <- value E2.

%{! 

## Preservation

With this encoding, we have to prove preservation explicitly, as the
type of ``step`` doesn't guarantee it.

!}%

pres : step E E' -> of E T -> of E' T -> type. %name pres Dpres.
%mode pres +Dstep +Dof -Dof.

- : pres
     (step/s Dstep)
     (of/s Dof)
     (of/s Dof')
     <- pres Dstep Dof Dof'.

- : pres
     (step/ifz/arg Dstep)
     (of/ifz ([x] [dx] Dof2 x dx) Dof1 Dof)
     (of/ifz ([x] [dx] Dof2 x dx) Dof1 Dof')
     <- pres Dstep Dof Dof'.

- : pres
     step/ifz/z
     (of/ifz _ Dof1 _)
     Dof1.

- : pres
     (step/ifz/s (_ : value E))
     (of/ifz ([x] [dx] Dof2 x dx) _ (of/s Dof))
     (Dof2 E Dof).

- : pres
     (step/app/fun Dstep1)
     (of/app Dof2 Dof1)
     (of/app Dof2 Dof1')
     <- pres Dstep1 Dof1 Dof1'.

- : pres
     (step/app/arg Dstep2 _)
     (of/app Dof2 Dof1)
     (of/app Dof2' Dof1)
     <- pres Dstep2 Dof2 Dof2'.

- : pres
     (step/app/beta-v _)
     (of/app
	Dof2
	(of/fun [f] [df] [x] [dx] (Dof1 f df x dx)))
     (Dof1
	_
	(of/fun [f] [df] [x] [dx] (Dof1 f df x dx))
	_
	Dof2).

%worlds () (pres Dstep Dof Dof').
%total Dstep (pres Dstep _ _).

%{! 

## Progress

!}%

val-or-step : exp -> type.		%name val-or-step Dvos.
vos/val : val-or-step E <- value E.
vos/step : val-or-step E <- step E _.

%{! 

### [Factoring lemmas](/wiki/output-factoring/)

!}%

prog/s
   : val-or-step E -> val-or-step (s E) -> type.
%mode prog/s +Dvos1 -Dvos2.

- : prog/s
     (vos/step Dstep)
     (vos/step (step/s Dstep)).

- : prog/s
     (vos/val Dval)
     (vos/val (value/s Dval)).

%worlds () (prog/s _ _).
%total {} (prog/s _ _).

prog/ifz
   :  of E nat
      -> val-or-step E
      -> {E1} {E2} (step (ifz E E1 ([x] E2 x)) E')
      -> type.
%mode prog/ifz +Dof +Dvos +E1 +E2 -Dstep.

- : prog/ifz _ (vos/step Dstep) _ _ (step/ifz/arg Dstep).
- : prog/ifz _ (vos/val value/z) _ _ step/ifz/z.
- : prog/ifz _ (vos/val (value/s Dval)) _ _ (step/ifz/s Dval).

%worlds () (prog/ifz _ _ _ _ _).
%total {} (prog/ifz _ _ _ _ _).

prog/app
   : of E1 (arr T2 T)
      -> val-or-step E1
      -> val-or-step E2
      -> step (app E1 E2) E'
      -> type.
%mode prog/app +Dof +Dvos1 +Dvos2 -Dstep.

- : prog/app
     _
     (vos/step Dstep1)
     _
     (step/app/fun Dstep1).

- : prog/app
     _
     (vos/val Dval1)
     (vos/step Dstep2)
     (step/app/arg Dstep2 Dval1).

- : prog/app
     _
     (vos/val Dval1)
     (vos/val Dval2)
     (step/app/beta-v Dval2).

%worlds () (prog/app _ _ _ _).
%total {} (prog/app _ _ _ _).

%{! 

### Main theorem

!}%

prog : of E T -> val-or-step E -> type. %name prog Dprog.
%mode prog +Dof -Dvos.

- : prog of/z (vos/val value/z).

- : prog (of/s Dof) Dvos'
     <- prog Dof Dvos
     <- prog/s Dvos Dvos'.

- : prog (of/ifz ([x] [dx] Dof2 x dx) Dof1 Dof) (vos/step Dstep)
     <- prog Dof Dvos
     <- prog/ifz Dof Dvos _ _ Dstep.

- : prog (of/fun _) (vos/val value/fun).

- : prog (of/app Dof2 Dof1) (vos/step Dstep)
     <- prog Dof1 Dvos1
     <- prog Dof2 Dvos2
     <- prog/app Dof1 Dvos1 Dvos2 Dstep.

%worlds () (prog _ _).
%total Dof (prog Dof _).

%{! 

And thus we have proved type safety for minml!

## Examples

!}%

%abbrev plus : exp = 
  fun nat (arr nat nat) 
   ([plus] [x] ifz x 
                  (fun nat nat ([_] [y] y))
                  ([predx] (fun nat nat ([_] [y] s (app (app plus predx) y))))).
%solve D : of plus T.

%abbrev mult : exp = 
  fun nat (arr nat nat) 
   ([mult] [x] (fun nat nat
		  [_] [y]
		  ifz y 
		      z
		  ([predy] app (app plus x) (app (app mult x) predy)))).
%solve Dmult : of mult T.

%abbrev fact : exp = 
  fun nat nat ([fact] [x] 
    ifz x 
       (s z) 
       ([predx] (app (app mult x) (app fact predx)))).  

%{! 
|check="decl" !}%
%solve Dfact : of fact T.

%{!  !}%

stepv : exp -> exp -> type.
stepv/v : stepv E E
	   <- value E.
stepv/s : stepv E E''
	   <- step E E'
	   <- stepv E' E''.

%solve D : stepv (app (app plus (s (s z))) (s (s z))) E.
%solve D : stepv (app (app mult (s (s (s z)))) (s (s z))) E.

%{! 
|check="decl" !}%
%solve D : stepv (app fact (s (s (s z)))) E.
%{!  !}%

%{! 

\{\{Summer school 2008
  |prev=Type safety for MinML (intrinsic encoding)
  |prevname=Type safety for MinML (intrinsic encoding)
  |next=Exercises 3
  |nextname=Exercises 3
\}\}

!}%

%{!
-----
This page was copied from the MediaWiki version of the Twelf Wiki.
If anything looks wrong, you can refer to the
[wayback machine's version here](https://web.archive.org/web/20240303030303/http://twelf.org/wiki/Summer_school_2008:Type_safety_for_MinML_(extrinsic_encoding)).
!}%
%%! title: "Reasoning from false"

%{!
Twelf's coverage checker often rules out contradictory cases for you (see, for example, the [Uniqueness lemmas](/wiki/uniqueness-lemma/)).  However, you will sometimes need to reason from contradictory assumptions yourself. You can do this by declaring an _uninhabited type_, proving that certain assumptions are contradictory, and then writing lemmas that conclude anything from a term of the uninhabited type. 

#
# The uninhabited type

&lt;twelf&gt;uninhabited : type. 
%freeze uninhabited. 
&lt;/twelf&gt;

Freezing the intentionally uninhabited type (which is also sometimes called ``void``, ``false``, or ``absurd``) is not strictly necessary, but it clarifies the programmer's intentâ€”Twelf will signal an error if any later part of the signature introduces a term of this type.

#
# Proving contradictions

We use some simple judgements on natural numbers to illustrate proving contradictions.

&lt;twelf&gt;
nat   : type. 
nat/z : nat.
nat/s : nat -&gt; nat.

nat-less   : nat -&gt; nat -&gt; type.
nat-less/z : nat-less nat/z (nat/s N).
nat-less/s : nat-less (nat/s N1) (nat/s N2)
              &lt;- nat-less N1 N2.

nat-plus   : nat -&gt; nat -&gt; nat -&gt; type.
nat-plus/z : nat-plus nat/z N N.
nat-plus/s : nat-plus (nat/s N1) N2 (nat/s N3)
              &lt;- nat-plus N1 N2 N3.
&lt;/twelf&gt;

#
#
# Contradiction by coverage checking

First, we prove that no number is less than zero:
&lt;twelf check="true"&gt;
nat-less-z-uninhabited : nat-less N nat/z 
                          -&gt; uninhabited
                          -&gt; type.
%mode nat-less-z-uninhabited +D1 -D2.
%worlds () (nat-less-z-uninhabited _ _).
%total \{\} (nat-less-z-uninhabited _ _).
&lt;/twelf&gt;
The coverage checker accepts this proof without any cases because there are no rules for deriving ``nat-less _ nat/z``.  This is the simplest way to create a contradiction.  

#
#
# Inductive contradictions

Some contradictions require an inductive argument.  For example, we can prove that if ``N1 &lt; N2`` then there is no ``N3`` such that ``N2 + N3 = N1``.
&lt;twelf check="true"&gt;
nat-less-plus-uninhabited : nat-less N1 N2
                             -&gt; nat-plus N2 N3 N1
                             -&gt; uninhabited
                             -&gt; type.
%mode nat-less-plus-uninhabited +D1 +D2 -D3.

- : nat-less-plus-uninhabited (nat-less/s NL) (nat-plus/s NP) DU
     &lt;- nat-less-plus-uninhabited NL NP DU.

%worlds () (nat-less-plus-uninhabited _ _ _).
%total (D1) (nat-less-plus-uninhabited D1 _ _).
&lt;/twelf&gt;

In this theorem, the coverage checker rules out all cases but the one where the first input is of the form
&lt;code&gt;
nat-less N1 N2
------------------------------nat-less/s
nat-less (nat/s N1) (nat/s N2)
&lt;/code&gt;
and the second input is of the form
&lt;code&gt;
nat-plus N2 N3 N1
----------------------------------nat-plus/s
nat-plus (nat/s N2) N3 (nat/s N1)
&lt;/code&gt;
To show that this remaining case is contradictory. we apply the inductive hypothesis on the subderivations to derive ``uninhabited``, and then return the resulting derivation of ``uninhabited`` to meet our proof obligations.

With this theorem, the coverage checker rules out all the base cases, so we are able to return a term of type ``uninhabited`` in the one remaining case by writing a loop.

#
# Reasoning from contradiction

Once you have a contradiction, it is easy to write a lemma that reasons from it.  For example, from ``uninhabited`` we can conclude that any two numbers are ``nat-less``:

&lt;twelf check="true"&gt;
uninhabited-nat-less : \{N1:nat\}\{N2:nat\}
                         uninhabited
                        -&gt; nat-less N1 N2
                        -&gt; type.
%mode uninhabited-nat-less +D1 +D2 +D3 -D4.
%worlds () (uninhabited-nat-less _ _ _ _).
%total \{\} (uninhabited-nat-less _ _ _ _).
&lt;/twelf&gt;

This proof requires no cases because any case would involve a canonical form of type ``uninhabited``.

This lemma could be used in a proof that requires coming up with a derivation of some ``less-than`` fact in contradictory circumstances.  

For an example where reasoning from contradiction is necessary, see the tutorial on [hereditary substitution for the STLC](/wiki/hereditary-substitution-for-the-stlc/).

\{\{tutorial\}\}
!}%

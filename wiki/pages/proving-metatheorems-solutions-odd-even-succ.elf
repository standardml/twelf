%%! title: "Solution: proofs about even and odd numbers"
%%! description: "Showing how adding one influences even and odd numbers (Part of the introduction to proving metatheorems in Twelf)"

%{!

_This page is part of the introduction to [proving metatheorems with Twelf](/wiki/proving-metatheorems-with-twelf/)._

Exercise 3 from [the part 1 exercises](/wiki/proving-metatheorems-summary-the-natural-numbers/): State and prove a theorem ``succ-even`` that shows that the successor of an even number is an odd number. After that, state and prove a theorem ``succ-odd`` that shows that the successor of an odd number is an even number.

Starting with the preliminaries, including the [definition of `odd` from exercise 2](/wiki/proving-metatheorems-solutions-define-odd/):

!}%

nat : type.
z   : nat.
s   : nat -> nat.

plus   : nat -> nat -> nat -> type.
plus-z : plus z N2 N2.
plus-s : plus (s N1) N2 (s N3)
          <- plus N1 N2 N3.

even   : nat -> type.
even-z : even z.
even-s : even (s (s N)) 
          <- even N.

odd   : nat -> type.
odd-1 : odd (s z).
odd-s : odd N -> odd (s (s N)).

%{! Because both the `even` and `odd` judgements are defined independently, it's necessary to perform induction on the `even` judgement to prove that the first theorem, and induction on the `odd` judgement to prove the second theorem: !}%

%{! ## The successor of an even number is odd !}%

%{!! begin checked !!}%
succ-even : even N -> odd (s N) -> type.
%mode succ-even +D1 -D2.

sez : succ-even even-z odd-1.

ses : succ-even (even-s EvenA) (odd-s OddA)
       <- succ-even EvenA OddA.

%worlds () (succ-even _ _).
%total D (succ-even D _).
%{!! end checked !!}%

%{! ## The successor of an odd number is even !}%

%{!! begin checked !!}%
succ-odd : odd N -> even (s N) -> type.
%mode succ-odd +D1 -D2.

so1 : succ-odd odd-1 (even-s even-z).

sos : succ-odd (odd-s OddA) (even-s EvenA)
       <- succ-odd OddA EvenA.

%worlds () (succ-odd _ _).
%total D (succ-odd D _).
%{!! end checked !!}%
